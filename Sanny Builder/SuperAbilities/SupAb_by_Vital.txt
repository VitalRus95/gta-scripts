//Supernatural abilities
/* Made by Vital */
//$PLAYER_ACTOR $PLAYER_GROUP $ONMISSION
//Byte, Char – 1; Short, Word – 2; Long, DWord, Float – 4
{$CLEO}
0000:

const
/* KEYS */
ND_HEAL = 49 //1
ND_SWITCH = 50 //2
ND_TELE = 51 //3
ND_GRAV = 52 //4
ND_NOCOPS = 53 //5
ND_PROTECT = 54 //6

DR_HEAL = 49 //1
DR_SPEED = 50 //2
DR_TELE = 51 //3
DR_JUMP = 52 //4
DR_NOCOPS = 53 //5
DR_PROTECT = 54 //6

FL_ENDIS = 18 //Crouch

/* VALUES */
GRAV_ADDR = 0x863984
NORM_GRAV = 0.008
NEG_GRAV = -0.1
STR_GRAV = 0.3
IMM_OFF = 0x42 //Actor's/Vehicle's immunities offset
HLT_OFF = 0x544 //Actor's max health offset
end // const

while true
wait 0
    if
    player.Defined(0)
    then
        if
        not actor.Driving($PLAYER_ACTOR) 
        then //NOT DRIVING
            if or
            8818: not actor $PLAYER_ACTOR in_air
            80E1: not  player 0 pressed_key FL_ENDIS
            then //ON FOOT
                if and
                8818: not actor $PLAYER_ACTOR in_air
                0AB0:   key_pressed ND_GRAV
                then
                gosub @Grav
                0AB1: call_scm_func @Wait 1 ND_GRAV
                else
                    if
                    0AB0:   key_pressed ND_SWITCH
                    then
                    gosub @Switch
                    0AB1: call_scm_func @Wait 1 ND_SWITCH
                    else
                        if and
                        82D8: not  actor $PLAYER_ACTOR current_weapon == 46
                        0AB0:   key_pressed ND_TELE
                        then
                        gosub @Tele
                        0AB1: call_scm_func @Wait 1 ND_TELE
                        else
                            if
                            0AB0:   key_pressed ND_PROTECT
                            then
                            gosub @Immune
                            0AB1: call_scm_func @Wait 1 ND_PROTECT
                            else
                                if
                                0AB0:   key_pressed ND_NOCOPS
                                then
                                gosub @NoCops
                                0AB1: call_scm_func @Wait 1 ND_NOCOPS
                                else
                                    if
                                    0AB0:   key_pressed ND_HEAL
                                    then
                                    gosub @Heal
                                    0AB1: call_scm_func @Wait 1 ND_HEAL
                                    else
                                        if
                                        0ADC:   test_cheat "SUPAB"
                                        then
                                        0A96: 1@ = actor $PLAYER_ACTOR struct
                                        1@ += IMM_OFF
                                        0A8D: 2@ = read_memory 1@ size 1 virtual_protect 0
                                        player.CanMove(0,0)
                                        0ADF: add_dynamic_GXT_entry "SPAB01" text "SuperAbilities by ~y~Vital"
                                        0ADF: add_dynamic_GXT_entry "SPAB02" text "~g~On foot:"
                                        0ADF: add_dynamic_GXT_entry "SPAB03" text "Health & Armour - 1"
                                        0ADF: add_dynamic_GXT_entry "SPAB04" text "Swap with ped - 2"
                                        0ADF: add_dynamic_GXT_entry "SPAB05" text "Teleport to marker - 3"
                                        0ADF: add_dynamic_GXT_entry "SPAB06" text "Gravity strike - 4"
                                        0ADF: add_dynamic_GXT_entry "SPAB07" text "Clear wanted level - 5"
                                        0ADF: add_dynamic_GXT_entry "SPAB08" text "SuperPlayer - 6"
                                        0ADF: add_dynamic_GXT_entry "SPAB09" text "~r~In vehicle:"
                                        0ADF: add_dynamic_GXT_entry "SPAB10" text "Repair - 1"
                                        0ADF: add_dynamic_GXT_entry "SPAB11" text "Speed boost (hold) - 2"
                                        0ADF: add_dynamic_GXT_entry "SPAB12" text "Teleport to marker - 3"
                                        0ADF: add_dynamic_GXT_entry "SPAB13" text "Jump - 4"
                                        0ADF: add_dynamic_GXT_entry "SPAB14" text "Clear wanted level - 5"
                                        0ADF: add_dynamic_GXT_entry "SPAB15" text "SuperCar - 6"
                                        0ADF: add_dynamic_GXT_entry "SPAB16" text "~b~Flying:"
                                        0ADF: add_dynamic_GXT_entry "SPAB17" text "Enable/Disable (in air) - ~k~~PED_DUCK~"
                                        0ADF: add_dynamic_GXT_entry "SPAB18" text "Forward/Backward - ~k~~GO_FORWARD~/~k~~GO_BACK~"
                                        0ADF: add_dynamic_GXT_entry "SPAB19" text "Up/Down - ~k~~PED_SPRINT~/~k~~PED_JUMPING~"
                                        0ADF: add_dynamic_GXT_entry "SPAB20" text "Left/Right - ~k~~GO_LEFT~/~k~~GO_RIGHT~"
                                        0ADF: add_dynamic_GXT_entry "SPAB21" text "Explode all cars - ~k~~PED_FIREWEAPON~"
                                        0ADF: add_dynamic_GXT_entry "SPAB22" text "Reset position - ~k~~PED_LOCK_TARGET~"
                                        08D4: 0@ = create_panel_with_title 'SPAB01' position 1.0 120.0 width 206.0 columns 3 interactive 0 background 1 alignment 1  // Imports
                                        08DB: set_panel 0@ column 0 header 'SPAB02' data 'SPAB03' 'SPAB04' 'SPAB05' 'SPAB06' 'SPAB07' 'SPAB08' 'DUMMY' 'DUMMY' 'DUMMY' 'DUMMY' 'DUMMY' 'DUMMY'  // Sunday // Monday // Tuesday // Wednesday // Thursday // Friday // Saturday
                                        08DB: set_panel 0@ column 1 header 'SPAB09' data 'SPAB10' 'SPAB11' 'SPAB12' 'SPAB13' 'SPAB14' 'SPAB15' 'DUMMY' 'DUMMY' 'DUMMY' 'DUMMY' 'DUMMY' 'DUMMY'  // Sunday // Monday // Tuesday // Wednesday // Thursday // Friday // Saturday
                                        08DB: set_panel 0@ column 2 header 'SPAB16' data 'SPAB17' 'SPAB18' 'SPAB19' 'SPAB20' 'SPAB21' 'SPAB22' 'DUMMY' 'DUMMY' 'DUMMY' 'DUMMY' 'DUMMY' 'DUMMY'  // Sunday // Monday // Tuesday // Wednesday // Thursday // Friday // Saturday
                                        09DB: set_panel 0@ column 0 width 130
                                        09DB: set_panel 0@ column 0 width 90
                                        09DB: set_panel 0@ column 0 width 206
                                            while player.Defined(0)
                                            wait 0
                                            0826: enable_hud 0
                                            0581: enable_radar 0
                                                if or
                                                actor.Driving($PLAYER_ACTOR)
                                                00E1:   player 0 pressed_key 15
                                                then
                                                break
                                                end
                                            end
                                        0826: enable_hud 1
                                        0581: enable_radar 1
                                        player.CanMove(0,1)
                                        0A8C: write_memory 1@ size 1 value 2@ virtual_protect 0
                                        08DA: remove_panel 0@
                                        0AE0: remove_dynamic_GXT_entry "SPAB01"
                                        0AE0: remove_dynamic_GXT_entry "SPAB02"
                                        0AE0: remove_dynamic_GXT_entry "SPAB03"
                                        0AE0: remove_dynamic_GXT_entry "SPAB04"
                                        0AE0: remove_dynamic_GXT_entry "SPAB05"
                                        0AE0: remove_dynamic_GXT_entry "SPAB06"
                                        0AE0: remove_dynamic_GXT_entry "SPAB07"
                                        0AE0: remove_dynamic_GXT_entry "SPAB08"
                                        0AE0: remove_dynamic_GXT_entry "SPAB09"
                                        0AE0: remove_dynamic_GXT_entry "SPAB10"
                                        0AE0: remove_dynamic_GXT_entry "SPAB11"
                                        0AE0: remove_dynamic_GXT_entry "SPAB12"
                                        0AE0: remove_dynamic_GXT_entry "SPAB13"
                                        0AE0: remove_dynamic_GXT_entry "SPAB14"
                                        0AE0: remove_dynamic_GXT_entry "SPAB15"
                                        0AE0: remove_dynamic_GXT_entry "SPAB16"
                                        0AE0: remove_dynamic_GXT_entry "SPAB17"
                                        0AE0: remove_dynamic_GXT_entry "SPAB18"
                                        0AE0: remove_dynamic_GXT_entry "SPAB19"
                                        0AE0: remove_dynamic_GXT_entry "SPAB20"
                                        0AE0: remove_dynamic_GXT_entry "SPAB21"
                                        0AE0: remove_dynamic_GXT_entry "SPAB22"
                                        end
                                    end
                                end
                            end
                        end
                    end
                end
            else //IN AIR
                if and
                0818:  actor $PLAYER_ACTOR in_air
                00E1:   player 0 pressed_key FL_ENDIS
                8A0C: not  player 0 on_jetpack
                then                
                gosub @Fly
                end
            end            
        else //DRIVING
            if
            0AB0:   key_pressed DR_HEAL
            then
            gosub @DR_Heal
            0AB1: call_scm_func @Wait 1 DR_HEAL
            else
                if
                0AB0:   key_pressed DR_SPEED
                then
                gosub @DR_Speed
                else
                    if
                    0AB0:   key_pressed DR_JUMP
                    then
                    gosub @DR_Jump
                    0AB1: call_scm_func @Wait 1 DR_JUMP
                    else
                        if
                        0AB0:   key_pressed DR_PROTECT
                        then
                        gosub @DR_Immune
                        0AB1: call_scm_func @Wait 1 DR_PROTECT
                        else
                            if
                            0AB0:   key_pressed DR_TELE
                            then
                            gosub @Tele
                            0AB1: call_scm_func @Wait 1 DR_TELE
                            else
                                if
                                0AB0:   key_pressed DR_NOCOPS
                                then
                                gosub @NoCops
                                0AB1: call_scm_func @Wait 1 DR_NOCOPS
                                end
                            end
                        end
                    end
                end
            end
        end
    end
end

/* WAIT FOR KEY RELEASE */

:Wait
repeat
wait 0
until 8AB0: not  key_pressed 0@
0AB2: ret 0

:Wait2
repeat
wait 0
until 80E1: not  player 0 pressed_key 0@
0AB2: ret 0

/* ON FOOT ACTIONS */

:Heal
2@ = 0
0A96: 0@ = actor $PLAYER_ACTOR struct
0@ += HLT_OFF 
0A8D: 0@ = read_memory 0@ size 4 virtual_protect 0
0092: 0@ = float 0@ to_integer
1@ = actor.Health($PLAYER_ACTOR)
if
001D:   0@ > 1@  // (int)
then
actor.Health($PLAYER_ACTOR) = 0@
2@ = 1 //Healing
end
0945: get_player 0 max_armour_to 0@
04DD: 1@ = actor $PLAYER_ACTOR armour
if
001D:   0@ > 1@  // (int)
then
actor.AddArmour($PLAYER_ACTOR,0@)
    if
    2@ == 1
    then
    2@ = 3 //Both
    else
    2@ = 2 //Armour
    end
end
if
2@ == 1
then
0ACA: show_text_box "Health."
else
    if
    2@ == 2
    then
    0ACA: show_text_box "Armour."
    else
        if
        2@ == 3
        then
        0ACA: show_text_box "Health & Armour."
        else
        0ACA: show_text_box "Player doesn't need health & armour!"
        end
    end
end
return

:Grav
0ACA: show_text_box "Gravity strike."
32@ = 0 
actor.LockInCurrentPosition($PLAYER_ACTOR,1)
0A1A: actor $PLAYER_ACTOR perform_walk_animation "endchat_02" IFP "PED" framedelta 4.0 loopA 0 lockX 0 lockY 0 lockF 0 -1 ms // versionC
0A8C: write_memory GRAV_ADDR size 4 value NEG_GRAV virtual_protect 0
0AA5: call 0x00439D80 num_params 0 pop 0
while true
wait 0
    if or
    32@ > 1000
    not player.Defined(0)
    then
    break
    end
end
0A8C: write_memory GRAV_ADDR size 4 value STR_GRAV virtual_protect 0
32@ = 0
while true
wait 0
    if or
    32@ > 1000
    not player.Defined(0)
    then
    break
    end
end
actor.LockInCurrentPosition($PLAYER_ACTOR,0)
0A8C: write_memory GRAV_ADDR size 4 value NORM_GRAV virtual_protect 0
return

:Switch
if
0AD2: 0@ = player 0 targeted_actor //IF and SET
then
actor.StorePos(0@,1@,2@,3@)
4@ = actor.Angle(0@)
actor.DestroyInstantly(0@)
actor.PutAt($PLAYER_ACTOR,1@,2@,3@)
actor.Angle($PLAYER_ACTOR) = 4@
018C: play_sound 1132 at 0.0 0.0 0.0
else
0ACA: show_text_box "Nobody targeted!"
end
return

:Tele
077E: get_active_interior_to 0@
if
0@ == 0
then
    if
    0AB6: store_target_marker_coords_to 5@ 6@ 7@ // IF and SET
    then    
        if
        actor.Driving($PLAYER_ACTOR)
        then
        3@ = actor.CurrentCar($PLAYER_ACTOR)
            if
            04C8:   actor $PLAYER_ACTOR driving_flying_vehicle
            then
            car.StorePos(3@,10@,8@,9@)
            02E3: 8@ = car 3@ speed
            end
        0A97: 2@ = car 3@ struct
        2@ += IMM_OFF
        0A8D: 4@ = read_memory 2@ size 1 virtual_protect 0
        end
    0A96: 0@ = actor $PLAYER_ACTOR struct
    0@ += IMM_OFF
    0A8D: 1@ = read_memory 0@ size 1 virtual_protect 0
    04E4: refresh_game_renderer_at 5@ 6@ 7@
    03CB: set_rendering_origin_at 5@ 6@ -100.0
        if
        actor.Driving($PLAYER_ACTOR)
        then  
            if
            056E:   car 3@ defined
            then
                if
                actor.InCar($PLAYER_ACTOR,3@)
                then
                0A8C: write_memory 2@ size 1 value 4@ virtual_protect 0
                gosub @VehPutAt 
                else
                gosub @VehPutAt
                end
            else
            gosub @VehPutAt 
            end 
        else
        actor.PutAt($PLAYER_ACTOR,5@,6@,-100.0)
        end
    0A8C: write_memory 0@ size 1 value 1@ virtual_protect 0
    0ACA: show_text_box "Teleport successful!"
    else
    0ACA: show_text_box "There's no waypoint!"
    end
else
0ACA: show_text_box "Player is not outside!"
end
return

:VehPutAt
if
04C8:   actor $PLAYER_ACTOR driving_flying_vehicle
then
02CE: 7@ = ground_z_at 5@ 6@ 7@
7@ += 500.0
actor.PutAt($PLAYER_ACTOR,5@,6@,7@)
car.SetSpeedInstantly(3@,8@)
else
actor.PutAt($PLAYER_ACTOR,5@,6@,-100.0)
end
return

:Immune
0A96: 0@ = actor $PLAYER_ACTOR struct
0@ += IMM_OFF
0A8D: 1@ = read_memory 0@ size 1 virtual_protect 0
    if or
    08B7:   test 1@ bit 3
    08B7:   test 1@ bit 4
    08B7:   test 1@ bit 5
    08B7:   test 1@ bit 6
    08B7:   test 1@ bit 8
    then
    actor.SetImmunities($PLAYER_ACTOR,0,0,0,0,0)
    0ACA: show_text_box "SuperPlayer - OFF."
    else
    actor.SetImmunities($PLAYER_ACTOR,1,1,1,1,1)
    0ACA: show_text_box "SuperPlayer - ON."
    end
return

:NoCops
if
010F:   player 0 wanted_level > 0
then
0ACA: show_text_box "Wanted level cleared."
09D4: suspend_wanted_level
else
0ACA: show_text_box "You have no wanted level!"
end
return

/* FLYING */

:Fly
0AB1: call_scm_func @Wait2 1 FL_ENDIS
if 
player.Defined(0)
then
    if and
    not actor.Driving($PLAYER_ACTOR)
    0818:  actor $PLAYER_ACTOR in_air
    8A0C: not  player 0 on_jetpack
    then
    0ACA: show_text_box "Fly - ON."
    0A96: 7@ = actor $PLAYER_ACTOR struct
    7@ += IMM_OFF
    0A8D: 8@ = read_memory 7@ size 1 virtual_protect 0
    083C: set_actor $PLAYER_ACTOR velocity_in_direction_XYZ 0.0 0.0 10.0    
        while gosub @IfFly
        wait 0
        actor.SetImmunities($PLAYER_ACTOR,1,1,1,1,1)    
        actor.LockInCurrentPosition($PLAYER_ACTOR,1)
            if
            not actor.Animation($PLAYER_ACTOR) == "FALL_skyDive"
            then
            0812: AS_actor $PLAYER_ACTOR perform_animation "FALL_skyDive" IFP "PED" framedelta 4.0 loopA 1 lockX 0 lockY 0 lockF 0 time -1
            end
                
            if
            00E1:   player 0 pressed_key 1 //Forward/Backward
            then                        
            068D: get_camera_position_to 0@ 1@ 2@ 
            actor.StorePos($PLAYER_ACTOR,3@,4@,5@)
            5@ += 1.0 
            00E2: get_player 0 key 1 state_to 6@
            0063: 0@ -= 3@  // (float)
            0063: 1@ -= 4@  // (float)
            0063: 2@ -= 5@  // (float)
                if
                6@ == -128 //Up
                then
                0@ *= -10.0
                1@ *= -10.0
                2@ *= -7.0
                else
                0@ *= 10.0
                1@ *= 10.0
                2@ *= 7.0
                end
            actor.LockInCurrentPosition($PLAYER_ACTOR,0)
            083C: set_actor $PLAYER_ACTOR velocity_in_direction_XYZ 0@ 1@ 2@
            0604: get_Z_angle_for_point 0@ 1@ store_to 2@
            actor.Angle($PLAYER_ACTOR) = 2@
            else
                if
                00E1:   player 0 pressed_key 16
                then
                actor.LockInCurrentPosition($PLAYER_ACTOR,0)
                083C: set_actor $PLAYER_ACTOR velocity_in_direction_XYZ 0.0 0.0 20.0
                else
                    if
                    00E1:   player 0 pressed_key 14
                    then
                    actor.LockInCurrentPosition($PLAYER_ACTOR,0)
                    083C: set_actor $PLAYER_ACTOR velocity_in_direction_XYZ 0.0 0.0 -20.0
                    else
                        if
                        00E1:   player 0 pressed_key 0
                        then
                        00E2: get_player 0 key 0 state_to 0@
                        actor.StorePos($PLAYER_ACTOR,1@,2@,3@)
                            if
                            0@ == 128 //Right
                            then
                            04C4: store_coords_to 4@ 5@ 6@ from_actor $PLAYER_ACTOR with_offset 20.0 0.0 0.0
                            0063: 4@ -= 1@  // (float)
                            0063: 5@ -= 2@  // (float)
                            0063: 6@ -= 3@  // (float)
                            else
                            04C4: store_coords_to 4@ 5@ 6@ from_actor $PLAYER_ACTOR with_offset -20.0 0.0 0.0
                            0063: 4@ -= 1@  // (float)
                            0063: 5@ -= 2@  // (float)
                            0063: 6@ -= 3@  // (float)
                            end
                        actor.LockInCurrentPosition($PLAYER_ACTOR,0)
                        083C: set_actor $PLAYER_ACTOR velocity_in_direction_XYZ 4@ 5@ 6@
                        else
                            if
                            00E1:   player 0 pressed_key 17
                            then
                            0AA5: call 0x00439D80 num_params 0 pop 0
                            else
                                if
                                00E1:   player 0 pressed_key 6
                                then
                                actor.StorePos($PLAYER_ACTOR,0@,1@,2@)                                
                                actor.PutAt($PLAYER_ACTOR,0@,1@,-100.0)
                                end
                            end
                        end
                    end
                end
            end
        end        
    actor.LockInCurrentPosition($PLAYER_ACTOR,0)
    0687: clear_actor $PLAYER_ACTOR task
    0ACA: show_text_box "Fly - OFF."
    0A8C: write_memory 7@ size 1 value 8@ virtual_protect 0
    0AB1: call_scm_func @Wait2 1 FL_ENDIS
    end
end
return

:IfFly
077E: get_active_interior_to 0@
if
player.Defined(0)
then
    if and
    0@ == 0     
    not actor.Driving($PLAYER_ACTOR)
    80E1: not  player 0 pressed_key FL_ENDIS
    82D8: not  actor $PLAYER_ACTOR current_weapon == 46
    8A0C: not  player 0 on_jetpack
    then   
    0485:  return_true
    else
    059A:  return_false
    end
else
059A:  return_false
end
return

/* IN VEHICLE ACTIONS */

:DR_Heal
0@ = actor.CurrentCar($PLAYER_ACTOR)
if
02BF:   car 0@ sunk
then
car.StorePos(0@,1@,2@,3@)
02C1: store_to 1@ 2@ 3@ car_path_coords_closest_to 1@ 2@ 3@
car.PutAt(0@,1@,2@,-100.0)
else
    if and
    81F3: not  car 0@ in_air
    01F4:   car 0@ flipped
    then
    car.StorePos(0@,1@,2@,3@)
    3@ = car.Angle(0@)
    car.PutAt(0@,1@,2@,-100.0)
    car.Angle(0@) = 3@
    end
end
if or
8185: not  car 0@ health >= 999
03C9:   car 0@ damaged
then
0A30: repair_car 0@
0ACA: show_text_box "Vehicle is repaired."
else
0ACA: show_text_box "Vehicle doesn't need repair."
end
return

:DR_Speed
0@ = actor.CurrentCar($PLAYER_ACTOR)
02E3: 1@ = car 0@ speed
if
1@ < 500.0
then
1@ *= 1.22
car.SetSpeedInstantly(0@,1@)
end
return

:DR_Jump
0@ = actor.CurrentCar($PLAYER_ACTOR)
07D5: set_car 0@ velocity_in_direction_XYZ 0.0 0.0 0.3 rotation_velocitiesXY 0.0 0.0 unk 0.0
018C: play_sound 1145 at 0.0 0.0 0.0
return

:DR_Immune
0@ = actor.CurrentCar($PLAYER_ACTOR)
0A97: 1@ = car 0@ struct
1@ += IMM_OFF
0A8D: 2@ = read_memory 1@ size 1 virtual_protect 0
if
2@ > 0
then
car.SetImmunities(0@,0,0,0,0,0)
01EC: make_car 0@ very_heavy 0
0ACA: show_text_box "SuperCar - OFF."
else
car.SetImmunities(0@,1,1,1,1,1)
01EC: make_car 0@ very_heavy 1
0ACA: show_text_box "SuperCar - ON."
end 
return